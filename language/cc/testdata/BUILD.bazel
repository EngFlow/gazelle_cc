load("@bazel_skylib//lib:paths.bzl", "paths")
load("@gazelle//:def.bzl", "gazelle_binary", "gazelle_generation_test")
load("//bazel:gazelle_compilation_tests.bzl", "gazelle_compilation_test")

gazelle_binary(
    name = "gazelle_for_testing",
    testonly = True,
    languages = [
        "@gazelle//language/proto",
        "//language/cc",
    ],
)

ALL_TEST_DIRS = [paths.dirname(p) for p in glob([
    "**/WORKSPACE",
    "**/MODULE.bazel",
])]

[gazelle_generation_test(
    name = test_dir,
    gazelle_binary = ":gazelle_for_testing",
    test_data = glob(
        include = [test_dir + "/**"],
    ),
) for test_dir in ALL_TEST_DIRS]

# TODO: include more tests
COMPILABLE_TEST_DIRS = [
    "cc_grpc_library",
    "subdirectory_basic",
    "virtual_include_paths",
]

[gazelle_compilation_test(
    name = test_dir + "_compilation",
    test_dir = test_dir,
) for test_dir in COMPILABLE_TEST_DIRS]
