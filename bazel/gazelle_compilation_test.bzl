"""
Checks that test data for gazelle_generation_test() makes sense, i.e., that the BUILD files generated by Gazelle allow
the workspace to be built successfully by Bazel.
"""

load("@bazel_binaries//:defs.bzl", "bazel_binaries")
load("@bazel_skylib//lib:paths.bzl", "paths")
load("@rules_bazel_integration_test//bazel_integration_test:defs.bzl", "bazel_integration_test", "integration_test_utils")

def _rename_input_file(ctx, root_dir, input_file):
    output_basename = "BUILD.bazel" if input_file.basename == "BUILD.out" else input_file.basename
    output_dir = paths.relativize(input_file.dirname, ctx.label.package)
    output_path = paths.join(root_dir, output_dir, output_basename)
    return ctx.actions.declare_file(output_path)

def _convert_directory_structure_impl(ctx):
    # Ignore BUILD.in files
    input_files = [file for file in ctx.files.test_data if file.basename != "BUILD.in"]

    # Rename BUILD.out to BUILD.bazel
    root_dir = ctx.attr.name + "_"
    output_files = [_rename_input_file(ctx, root_dir, input_file) for input_file in input_files]

    # Copy file contents
    for input_file, output_file in zip(input_files, output_files):
        ctx.actions.run_shell(
            mnemonic = "GazelleTestDataConversion",
            inputs = [input_file],
            outputs = [output_file],
            arguments = [input_file.path, output_file.path],
            command = 'cp "$1" "$2"',
        )

    return DefaultInfo(files = depset(output_files))

convert_directory_structure = rule(
    doc = """
    Prepares a test workspace intended for gazelle_generation_test() to make it buildable by Bazel. To achieve this, all
    BUILD.out files are renamed to BUILD.bazel files.
    """,
    implementation = _convert_directory_structure_impl,
    attrs = {
        "test_data": attr.label_list(
            allow_files = True,
            mandatory = True,
            doc = "Workspace contents intended for gazelle_generation_test()",
        ),
    },
)

def gazelle_compilation_test(name, test_data, **kwargs):
    """
    gazelle_compilation_test is a macro complementary to gazelle_generation_test.
    
    It accepts the same input file structure as gazelle_generation_test, but instead of generating BUILD files from
    scratch, it assumes that BUILD files are already present (i.e., gazelle has already been run) and simply verifies
    that the existing BUILD files allow the workspace to be built successfully by Bazel.

    Args:
        name: The name of the test.
        test_data: A list of target of the test data files you will pass to the test.
        **kwargs: Attributes that are passed directly to the bazel_integration_test macro.
    """
    workspace_name = name + "_workspace"

    convert_directory_structure(
        name = workspace_name,
        test_data = test_data,
        testonly = True,
    )

    # Resolve as parent directory of the shallowest file path
    workspace_path = paths.dirname(min(test_data, key = lambda p: len(p.split("/"))))

    bazel_integration_test(
        name = name,
        bazel_binaries = bazel_binaries,
        bazel_version = bazel_binaries.versions.current,
        test_runner = "//bazel:bazel_builder",
        workspace_files = [":" + workspace_name],
        workspace_path = workspace_path,
        **kwargs
    )

def gazelle_compilation_tests(
        name,
        test_data_map,
        tags = integration_test_utils.DEFAULT_INTEGRATION_TEST_TAGS,
        **kwargs):
    """_summary_

    Args:
        name: The name of the test suite.
        test_data_map: A map from subtest names to the test data passed to each single gazelle_compilation_test.
        tags: Tags to apply to the test suite and all subtests.
        **kwargs: Attributes that are passed directly to each gazelle_compilation_test and to the test_suite.
    """
    for subtest_name, test_data in test_data_map.items():
        gazelle_compilation_test(
            name = subtest_name,
            test_data = test_data,
            tags = tags,
            **kwargs
        )

    native.test_suite(
        name = name,
        tests = [":" + subtest_name for subtest_name in test_data_map.keys()],
        tags = tags,
        **kwargs
    )
